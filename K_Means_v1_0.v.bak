
`timescale 1 ns / 1 ps

module K_Means_v1_0 #
  (
   // Users to add parameters here
   parameter integer NUM_OF_MAPPERS = 5,
   parameter integer NUM_OF_REDUCERS = 4,
   parameter integer PRECISION = 16, // number of bits used for each dimension
   parameter integer DIMENSION = 2,
   parameter integer K = 5,
   parameter integer DATA_WIDTH = PRECISION*DIMENSION,
   parameter integer CENTRE_WIDTH = 16,
   parameter integer SUM_WIDTH = 64,
   parameter integer KEY_WIDTH = 16,
   parameter integer BRAM_DEPTH = 8,
   parameter integer ITERATIONS = 2,
   // User parameters ends
   // Do not modify the parameters beyond this line


   // Parameters of Axi Slave Bus Interface S_AXIS
   parameter integer C_S_AXIS_TDATA_WIDTH = 32
   )
   (
    // Users to add ports here

    // User ports ends
    // Do not modify the ports beyond this line


    // Ports of Axi Slave Bus Interface S_AXIS
    input wire 					s_axis_aclk,
    input wire 					s_axis_aresetn,
    output wire 				s_axis_tready,
    input wire [C_S_AXIS_TDATA_WIDTH-1 : 0] 	s_axis_tdata,
    input wire [(C_S_AXIS_TDATA_WIDTH/8)-1 : 0] s_axis_tstrb,
    input wire 					s_axis_tlast,
    input wire 					s_axis_tvalid
    );
   // Instantiation of Axi Bus Interface S_AXIS
   /*K_Means_v1_0_S_AXIS # ( 
    .C_S_AXIS_TDATA_WIDTH(C_S_AXIS_TDATA_WIDTH)
    ) K_Means_v1_0_S_AXIS_inst (
    .S_AXIS_ACLK(s_axis_aclk),
    .S_AXIS_ARESETN(s_axis_aresetn),
    .S_AXIS_TREADY(s_axis_tready),
    .S_AXIS_TDATA(s_axis_tdata),
    .S_AXIS_TSTRB(s_axis_tstrb),
    .S_AXIS_TLAST(s_axis_tlast),
    .S_AXIS_TVALID(s_axis_tvalid)
    );
    */
   // Add user logic here
   function integer clogb2 (input integer bit_depth);
               begin
         for(clogb2=0; bit_depth>0; clogb2=clogb2+1)
           bit_depth = bit_depth >> 1;
      end
   endfunction
   
   // FIND THE LOG2 OF THE DEPTH OF THE RAM        
   localparam addr_bits = clogb2(K-1);
   localparam map_bits = clogb2(NUM_OF_MAPPERS);
   localparam bram_bits = clogb2(BRAM_DEPTH-1);
   
   localparam NUM_OF_PARTITIONERS = NUM_OF_MAPPERS;
   // User logic ends
   reg [C_S_AXIS_TDATA_WIDTH-1:0] data_in; // data coming in from the DMA
   
   // SIGNALS RELATED TO MERGE BLOCK
   wire 			  merge_done_out, merge_valid_in;
   wire [addr_bits-1:0] 	  merge_addr_out;
   wire [(DIMENSION*CENTRE_WIDTH)-1:0] merge_data_out;
   wire 			       merge_valid_out;
   wire 			       merge_ready_out;
   wire 			       init_mem;
   
   
   // INTERNAL WIRE FOR INPUT_CONVERTER (IC) BLOCK
   
   wire 			       ic_valid;
   wire 			       ic_ready;
   wire 			       ic_tlast;
   
   // INTERNAL WIRES FOR DATA INPUT FIFO
   
   wire [(DIMENSION*PRECISION)-1:0]    fifo_data_in;
   wire [(DIMENSION*PRECISION)-1:0]    fifo_data_out;
   wire 			       fifo_wren;
   wire 			       fifo_rden;
   wire 			       fifo_empty;
   wire 			       fifo_full;
   
   // INTERNAL WIRES FOR DATA_CONTROLLER BLOCK
   wire 			       dc_fifo_empty;
   wire [NUM_OF_MAPPERS-1:0] 	       request;
   wire [NUM_OF_MAPPERS-1:0] 	       grant;
   wire [NUM_OF_MAPPERS-1:0] 	       queued;
   
   
   // CONTROL SIGNALS FROM FIFOS
   wire [NUM_OF_MAPPERS-1:0] 	       fifo_val_full;
   wire [(NUM_OF_MAPPERS*DIMENSION*PRECISION)-1 :0] fifo_val_data_out;
   wire [NUM_OF_MAPPERS-1:0] 			    fifo_val_valid_out;
   
   wire [NUM_OF_MAPPERS-1:0] 			    fifo_key_full;
   wire [(NUM_OF_MAPPERS*16)-1 :0] 		    fifo_key_data_out;
   wire [NUM_OF_MAPPERS-1:0] 			    fifo_key_valid_out;
   
   genvar 					    i,j;
   //reg [map_bits-1:0] write_data [(CENTRE_WIDTH*DIMENSION)-1:0];
   //reg [map_bits-1:0] write_addr [addr_bits-1:0];
   wire [((NUM_OF_MAPPERS-1)*CENTRE_WIDTH*DIMENSION)-1:0] write_data;
   wire [((NUM_OF_MAPPERS-1)*addr_bits)-1:0] 		  write_addr; 
   
   wire [NUM_OF_MAPPERS-1:0] 				  write_en;
   wire [NUM_OF_MAPPERS-1:0] 				  write_done;
   
   
   // SIGNALS VALUE FIFO IN
   wire [(NUM_OF_PARTITIONERS*DIMENSION*PRECISION)-1:0]   i_fifo_val_data;
   wire [NUM_OF_PARTITIONERS-1:0] 			  i_fifo_val_empty;
   wire [NUM_OF_PARTITIONERS-1:0] 			  o_fifo_val_rden;
   
   // SIGNALS KEY FIFO IN
   wire [(NUM_OF_PARTITIONERS*16)-1:0] 			  i_fifo_key_data;
   wire [NUM_OF_PARTITIONERS-1:0] 			  i_fifo_key_empty;
   wire [NUM_OF_PARTITIONERS-1:0] 			  o_fifo_key_rden;
   
   
   
   // PARTITION BLOCK SIGNALS
   wire [(NUM_OF_PARTITIONERS*DIMENSION*PRECISION)-1:0]   o_value_data;
   wire [(NUM_OF_PARTITIONERS*KEY_WIDTH)-1:0] 		  o_mem_index;
   wire [(NUM_OF_PARTITIONERS*NUM_OF_REDUCERS)-1:0] 	  o_request;
   
   // REDUCE_ARBITER BLOCK SIGNALS
   
   wire [(NUM_OF_REDUCERS*NUM_OF_PARTITIONERS)-1:0] 	  o_acknowledged; // one cycle latency of grant
   wire [NUM_OF_REDUCERS-1:0] 				  o_KV_valid; // input to start reducer
   wire [(NUM_OF_REDUCERS*PRECISION*DIMENSION)-1:0] 	  o_value_data_out;
   wire [(NUM_OF_REDUCERS*KEY_WIDTH)-1:0] 		  o_key_data_out;
   
   // REDUCE BLOCK SIGNALS
   wire [NUM_OF_REDUCERS-1:0] 				  i_reduce_en;
   wire [NUM_OF_REDUCERS-1:0] 				  o_ce_reducers;
   wire [(NUM_OF_REDUCERS*SUM_WIDTH*DIMENSION)-1 : 0 ] 	  o_sum_pts;
   wire [(NUM_OF_REDUCERS*32)-1 : 0 ] 			  o_count_pts;
   wire [BRAM_BITS-1:0] 				  o_raddress_reducers;
   wire [(NUM_OF_REDUCERS*32)-1:0] 			  o_reduce_counters;

   // ACK BLOCK
   wire [(NUM_OF_PARTITIONERS*NUM_OF_REDUCERS)-1:0] 	  i_request;   
   wire [NUM_OF_PARTITIONERS-1:0] 			  o_ack;
   
   always @(posedge s_axis_aclk )
     begin
        if (!s_axis_aresetn)
          data_in <= {C_S_AXIS_TDATA_WIDTH{1'b0}};
        else if (s_axis_tready && s_axis_tvalid)
          data_in <= s_axis_tdata;
        else
          data_in <= data_in;
     end
   
   Scheduler  #(
		.DIMENSION(DIMENSION),
		.PRECISION(PRECISION),
		.DATA_WIDTH(DATA_WIDTH),
		.K(K),
		.ITERATIONS(ITERATIONS)
		//.ADDR_BITS(addr_bits)
		) fsm (
                       .clk(s_axis_aclk),
                       .reset_n(s_axis_aresetn),
                       //.idle(),
                       //.ready(),
                       // .start(),
		       .o_init_mem(init_mem),
                       .i_mem_update_done(merge_done_out),
                       .i_mem_map_done(write_done[NUM_OF_MAPPERS-1]),
                       .i_mem_update_ready(merge_ready_out),
		       .o_mem_update_valid(merge_valid_in),
                       
		       

                       .i_ic_ready(ic_ready),
		       .o_ic_valid(ic_valid),
                       .o_ic_tlast(ic_tlast),

                       //.read_next(),
                       .S_AXIS_TVALID(s_axis_tvalid),
                       .S_AXIS_TLAST(s_axis_tlast),
                       .S_AXIS_TREADY(s_axis_tready)
                       
                       
                       );
   
   // INPUT_CONVERTER INSTANTIATION
   // THIS MODULE CONVERTS THE 256-bit S_AXIS_DATA
   // INPUT WORD TO 256-bit OUTPUT WORD OR HIGHER
   input_converter #(
      
		     .INPUT_WIDTH(C_S_AXIS_TDATA_WIDTH),
		     .PRECISION(PRECISION),
		     .DIMENSION(DIMENSION)//,
      
		     ) i_c (
			    .clk(s_axis_aclk),
			    .reset_n(s_axis_aresetn),
			    .i_fifo_full(fifo_full),
			    .s_axis_tdata(s_axis_tdata),
			    .s_axis_tlast(ic_tlast),
			    .s_axis_tvalid(ic_valid),
			    .s_axis_tready(ic_ready),
			    .o_fifo_data(fifo_data_in),
			    .o_fifo_write(fifo_wren)
			    
			    );
   
   
   // FIFO DEFINITION
   // THE INPUT TO THIS FIFO IS INPUT_COVERTER WHICH CONVERTS
   // THE WIDTH OF AXI_DATA_INPUT TO APPROPRIATE WIDTH AND
   // PUSH THE NEW DATA POINT IN THE FIFO FOR PROCESSING 
   fifo # (
           .C_WIDTH(PRECISION*DIMENSION),
           .C_LOG_FIFO_DEPTH(5)
      
           ) ff( 
		 .clk(s_axis_aclk), 
		 .rst(!s_axis_aresetn), 
		 .buf_in(fifo_data_in), 
		 .buf_out(fifo_data_out), 
		 .wr_en(fifo_wren), 
		 .rd_en(fifo_rden), 
		 .buf_empty(fifo_empty), 
		 .buf_full(fifo_full), 
		 .fifo_counter() 
		 );
   
   
   // read enable is high if there is valid data in both data and dc fifo
   assign fifo_rden = !fifo_empty && !dc_fifo_empty;
   
   // DATA_CONTROLLER INSTANTIATION
   // THIS BLOCK MONITORS DATA TRAFFIC
   // SCHEDULE IDLE MAPPERS TO PROCESS IF THERE ARE 
   // ANY VALID DATA INPUT AVAILABLE
   data_controller #(
                     .NUM_OF_MAPPERS(NUM_OF_MAPPERS)
      
                     ) dc (
			   .clock(s_axis_aclk),
			   .reset_n(s_axis_aresetn),
			   .fifo_re(fifo_rden),
			   .request(request),
			   .grant(grant),
			   .queued(queued),
			   .fifo_empty(dc_fifo_empty)
			   );
   
   
   // GENERATE BLOCK FOR N MAPPERS
   generate
      for (i = 0 ; i < NUM_OF_MAPPERS; i = i + 1 )
        begin: map
           if (i == 0)
             begin
                map  #(
                       .DIMENSION(DIMENSION),
                       .PRECISION(PRECISION),
                       .CENTRE_WIDTH(CENTRE_WIDTH),
                       .DATA_WIDTH(DATA_WIDTH),
                       .K(K),
                       .ADDR_BITS(addr_bits)
                       ) mapper (
				 .clock(s_axis_aclk),
				 .reset_n(s_axis_aresetn),
				 .write_addr_in(merge_addr_out),
				 .write_data_in(merge_data_out),
				 .we_in(merge_valid_out),
				 .write_done_in(merge_done_out),
				 .write_addr_out(write_addr[((i+1)*addr_bits)-1-:addr_bits]),
				 .write_data_out(write_data[((i+1)*DIMENSION*CENTRE_WIDTH)-1 -: DIMENSION*CENTRE_WIDTH]),
				 .we_out(write_en[i]),
				 .write_done_out(write_done[i]),
				 //CONTROL SIGNALS FROM DATA CONTROLLER
				 .grant(grant[i]),
				 .request(request[i]),
				 .queued(queued[i]),
				 // DATA INPUT
				 .value_data_in(fifo_data_out),
				 
				 // CONTROL SIGNALS FROM FIFOS
				 .fifo_val_full(fifo_val_full[i]),
				 .fifo_val_data_out(fifo_val_data_out[((i+1)*DIMENSION*PRECISION)-1-:DIMENSION*PRECISION]/*fifo_val_data_out*/),
				 .fifo_val_valid_out(fifo_val_valid_out[i]),
				 .fifo_key_full(fifo_key_full[i]),
				 .fifo_key_data_out(fifo_key_data_out[((i+1)*16)-1-:16]),
				 .fifo_key_valid_out(fifo_key_valid_out[i])
				 
				 );        
             end
           else if ( i == NUM_OF_MAPPERS-1)
             begin
		map  #(
                       .DIMENSION(DIMENSION),
                       .PRECISION(PRECISION),
                       .CENTRE_WIDTH(CENTRE_WIDTH),
                       .DATA_WIDTH(DATA_WIDTH),
                       .K(K),
                       .ADDR_BITS(addr_bits)
                       ) mapper (
				 .clock(s_axis_aclk),
				 .reset_n(s_axis_aresetn),
				 .write_addr_in(write_addr[((i)*addr_bits)-1-:addr_bits]),
				 .write_data_in(write_data[((i)*DIMENSION*CENTRE_WIDTH)-1 -: DIMENSION*CENTRE_WIDTH]),
				 .we_in(write_en[i-1]),
				 .write_done_in(write_done[i-1]),
				 .write_addr_out(),
				 .write_data_out(),
				 .we_out(write_en[i]),
				 .write_done_out(write_done[i]),
				 //CONTROL SIGNALS FROM DATA CONTROLLER
				 .grant(grant[i]),
				 .request(request[i]),
				 .queued(queued[i]),
				 // DATA INPUT
				 .value_data_in(fifo_data_out),
				 
				 // CONTROL SIGNALS FROM FIFOS
				 .fifo_val_full(fifo_val_full[i]),
				 .fifo_val_data_out(fifo_val_data_out[((i+1)*DIMENSION*PRECISION)-1-:DIMENSION*PRECISION]/*fifo_val_data_out*/),
				 .fifo_val_valid_out(fifo_val_valid_out[i]),
				 .fifo_key_full(fifo_key_full[i]),
				 .fifo_key_data_out(fifo_key_data_out[((i+1)*16)-1-:16]),
				 .fifo_key_valid_out(fifo_key_valid_out[i])
				 );        
             end
           
           else
             begin
                map  #(
                       .DIMENSION(DIMENSION),
                       .PRECISION(PRECISION),
                       .CENTRE_WIDTH(CENTRE_WIDTH),
                       .DATA_WIDTH(DATA_WIDTH),
                       .K(K),
                       .ADDR_BITS(addr_bits)
                       ) mapper (
				 .clock(s_axis_aclk),
				 .reset_n(s_axis_aresetn),
				 .write_addr_in(write_addr[((i)*addr_bits)-1-:addr_bits]),
				 .write_data_in(write_data[((i)*DIMENSION*CENTRE_WIDTH)-1 -: DIMENSION*CENTRE_WIDTH]),
				 .we_in(write_en[i-1]),
				 .write_done_in(write_done[i-1]),
				 .write_addr_out(write_addr[((i+1)*addr_bits)-1-:addr_bits]),
				 .write_data_out(write_data[((i+1)*DIMENSION*CENTRE_WIDTH)-1 -: DIMENSION*CENTRE_WIDTH]),
				 .we_out(write_en[i]),
				 .write_done_out(write_done[i]),
				 //CONTROL SIGNALS FROM DATA CONTROLLER
				 .grant(grant[i]),
				 .request(request[i]),
				 .queued(queued[i]),
				 // DATA INPUT
				 .value_data_in(fifo_data_out),
				 
				 // CONTROL SIGNALS FROM FIFOS
				 .fifo_val_full(fifo_val_full[i]),
				 .fifo_val_data_out(fifo_val_data_out[((i+1)*DIMENSION*PRECISION)-1-:DIMENSION*PRECISION]/*fifo_val_data_out*/),
				 .fifo_val_valid_out(fifo_val_valid_out[i]),
				 
				 .fifo_key_full(fifo_key_full[i]),
				 .fifo_key_data_out(fifo_key_data_out[((i+1)*16)-1-:16]),
				 .fifo_key_valid_out(fifo_key_valid_out[i])
				 
				 );        
             end
        end
   endgenerate
   

   // GENERATE BLOCK FOR CREATING  FIFO FOR MAPPER KEY VALUE OUPUT
   // THESE ARE FEED INTO THE PARTITIONER AND AFTER THE REDUCER
   generate
      for (i = 0 ; i < NUM_OF_MAPPERS; i = i + 1 )
        begin: fifo
           fifo # (
		   .C_WIDTH(16),
		   .C_LOG_FIFO_DEPTH(5)
              
		   ) key( 
			  .clk(s_axis_aclk), 
			  .rst(!s_axis_aresetn), 
			  .buf_in(fifo_key_data_out[((i+1)*KEY_WIDTH)-1-:KEY_WIDTH]), 
			  .buf_out( i_fifo_key_data[((i+1)*KEY_WIDTH)-1-:KEY_WIDTH]), 
			  .wr_en(fifo_key_valid_out[i]), 
			  .rd_en(o_fifo_key_rden[i]), 
			  .buf_empty(i_fifo_key_empty[i]), 
			  .buf_full(fifo_key_full[i]), 
			  .fifo_counter() 
			  );
           fifo # (
		   .C_WIDTH(PRECISION*DIMENSION),
		   .C_LOG_FIFO_DEPTH(5)
              
		   ) value ( 
			     .clk(s_axis_aclk), 
			     .rst(!s_axis_aresetn), 
			     .buf_in(fifo_val_data_out[((i+1)*DIMENSION*PRECISION)-1-:DIMENSION*PRECISION]), 
			     .buf_out( i_fifo_val_data[((i+1)*DIMENSION*PRECISION)-1-:DIMENSION*PRECISION]), 
			     .wr_en(fifo_val_valid_out[i]), 
			     .rd_en( o_fifo_val_rden[i]), 
			     .buf_empty( i_fifo_val_empty[i]), 
			     .buf_full(fifo_val_full[i]), 
			     .fifo_counter() 
			     );
           
	end
      
   endgenerate
   
   // GENERATE BLOCK FOR PARTITIONERS
   
   generate 
      for ( i = 0 ; i < NUM_OF_PARTITIONERS; i = i + 1 )
	begin: partition
	   
           partition #(
                       //     .NUM_OF_PARTITIONERS = ,
                       .NUM_OF_REDUCERS(NUM_OF_REDUCERS),
                       .PRECISION(PRECISION),
                       .DIMENSION (DIMENSION)
	      
                       )par(
			    .clock(s_axis_aclk),
			    .reset_n(s_axis_aresetn),
			    
			    .i_fifo_val_data(i_fifo_val_data[((i+1)*DIMENSION*PRECISION)-1-:DIMENSION*PRECISION]),
			    .i_fifo_val_empty(i_fifo_val_empty[i]),
			    .o_fifo_val_rden(o_fifo_val_rden[i]),
			    
			    .i_fifo_key_data(i_fifo_key_data[((i+1)*16)-1-:16]),
			    .i_fifo_key_empty(i_fifo_key_empty[i]),
			    .o_fifo_key_rden(o_fifo_key_rden[i]),
			    
			    .i_acknowledged(o_ack[i]),
			    .o_request(o_request[((i+1)*NUM_OF_REDUCERS)-1-:NUM_OF_REDUCERS]),
			    
			    .o_value_data(o_value_data[((i+1)*DIMENSION*PRECISION)-1-:DIMENSION*PRECISION]),
			    .o_mem_index(o_mem_index[((i+1)*KEY_WIDTH)-1-:KEY_WIDTH])
			    
			    
			    );
	end // block: partition
   endgenerate
   
   // ACKNOWLEDGEMENT BLOCK
   // THIS BLOCK TAKES ALL THE ACKNOWLEDGEMENT SIGNALS FROM ALL THE REDUCERS
   // AND COMBINES RE-ROUTES THEM TO THE CORRECT PARTITIONER
   acknowledgement #(
                     .NUM_OF_REDUCERS(NUM_OF_REDUCERS),
                     .NUM_OF_PARTITIONERS(NUM_OF_PARTITIONERS)
      
                     ) ack (
			    .clock(s_axis_aclk),
			    .reset_n(s_axis_aresetn),
			    
			    .i_ack(o_acknowledged),
			    .o_ack(o_ack)
			    );
   
   
   
   // REARRANGE THE REQUEST SIGNALS FROM PARTITIONER TO FEED TO THE REDUCE ARBITERS
   // PUTS THE ACKNOWLEDGEMENT FOR EACH REDUCER COMING FROM DIFFERENT PARTITIONERS
   // CONSECUTIVELY
   generate
      for ( i = 0 ; i < NUM_OF_REDUCERS; i = i + 1 )
	begin:routing
           for ( j = 0 ; j < NUM_OF_PARTITIONERS; j = j + 1 )
             begin
		assign i_request[(i*NUM_OF_PARTITIONERS)+j] = o_request[(j*NUM_OF_REDUCERS)+i];
             end
           
	end
   endgenerate
   
   // GENERATE BLOCK FOR REDUCE ARBITER
   // THESE ARBITERS ARBITERATE THE REQUEST TO EACH REDUCER
   generate 
      for ( i = 0 ; i < NUM_OF_REDUCERS; i = i + 1 )
	begin: arbiter
           reduce_arbiter #(
			    .NUM_OF_PARTITIONERS(NUM_OF_PARTITIONERS),
			    .PRECISION(PRECISION),
			    .DIMENSION(DIMENSION)
              
			    )_reduce(
				     .clock(s_axis_aclk),
				     .reset_n(s_axis_aresetn),
				     .i_request(i_request[((i+1)*NUM_OF_PARTITIONERS)-1-:NUM_OF_PARTITIONERS]),
				     .i_reduce_en(i_reduce_en[i]),
				     .i_value_data_in(o_value_data),
				     .i_key_data_in(o_mem_index),
				     
				     .o_acknowledged(o_acknowledged[((i+1)*NUM_OF_PARTITIONERS)-1-:NUM_OF_PARTITIONERS]), // specify the request is acknowledged
				     .o_value_data_out(o_value_data_out[((i+1)*DIMENSION*PRECISION)-1-:DIMENSION*PRECISION]),
				     .o_key_data_out(o_key_data_out[((i+1)*KEY_WIDTH)-1-:KEY_WIDTH]),
				     .o_KV_valid(o_KV_valid[i])
				     );
	end // block: red
      
   endgenerate

   
   // GENERATE BLOCK FOR REDUCERS
   generate
      for ( i = 0 ; i < NUM_OF_REDUCERS; i = i + 1 )
	begin: reduce
           reduce #(
		    .DIMENSION(DIMENSION),
		    .PRECISION(PRECISION),
		    .SUM_WIDTH(SUM_WIDTH),
		    .ADDR_BITS(addr_bits), // bits required for number of cluster centres
		    .BRAM_DEPTH(8)
              
		    ) red  (
			    .clock(s_axis_aclk),
			    .reset_n(s_axis_aresetn),
			    .merge(1'b0), // send by the scheduler to indicate centres are being updated
			    .merge_done(1'b0), // restart for next iteration
			    
			    .i_raddress(o_raddress_reducers),
			    .i_ce(o_ce_reducers[i]),
			    .o_sum_data(o_sum_pts[((i+1)*DIMENSION*SUM_WIDTH)-1 -: DIMENSION*SUM_WIDTH]),
			    .o_counter_data(o_count_pts[((i+1)*32)-1 -: 32]),
			    .o_reduce_counters(o_reduce_counters[((i+1)*32)-1-:32]),
			    
			    .i_value_data(o_value_data_out[((i+1)*DIMENSION*PRECISION)-1-:DIMENSION*PRECISION]),
			    .i_key_data(o_key_data_out[((i+1)*KEY_WIDTH)-1-:KEY_WIDTH]),
			    .i_KV_valid(o_KV_valid[i]),
			    .o_reducer_ready(i_reduce_en[i])
			    
			    );
	end // block: reduce
   endgenerate
   
   
   merge #(
	   .INPUT_WIDTH(C_S_AXIS_TDATA_WIDTH), // input from dma
	   .ADDR_BITS(addr_bits),
	   .SUM_WIDTH(SUM_WIDTH),
	   .CENTRE_WIDTH(CENTRE_WIDTH), // with respect to the centres
	   .DIMENSION(DIMENSION),
	   .NUMBER_OF_REDUCERS(NUM_OF_REDUCERS),
	   .K(K),
	   .BRAM_BITS(bram_bits)
           //.OUTPUT_WIDTH = DIMENSION*PRECISION,
           //.MAX(INPUT_WIDTH/OUTPUT_WIDTH),
           ) merger
     (
      .clock(s_axis_aclk),
      .reset_n(s_axis_aresetn),
      .i_start(merge_valid_in),
      .i_sum_pts(o_sum_pts),
      .i_count_pts(o_count_pts),
      .o_ce_reducers(o_ce_reducers),
      .o_raddress_reducers(o_raddress_reducers),
      .o_centre_wdata(merge_data_out),
      .o_centre_address(merge_addr_out),
      .o_centre_we(merge_valid_out),
      .o_done(merge_done_out),
      .axis_data_in(s_axis_tdata),
      .axis_ready_out(merge_ready_out),
      .i_init(init_mem)
      
      );



   accumulator # (
		  .NUM_OF_REDUCERS(NUM_OF_REDUCERS)
		  ) accum (
			   .clock(s_axis_aclk),
			   .reset_n(s_axis_aresetn),
			   .i_reduce_counters(o_reduce_counters),
			   .i_res(),
			   .o_sum()
			   );
       
   
   // THE MASTER RAM WHERE GETS UPDATED AFTER EACH K-MEANS ITERATION
   bram # (
           .C_WIDTH(DIMENSION*CENTRE_WIDTH),
           .C_LOG_DEPTH(addr_bits)
           ) master_ram (
			 .i_clk(s_axis_aclk),
			 .i_waddr(merge_addr_out),
			 .i_wdata(merge_data_out),
			 .i_wen(merge_valid_out),
			 .i_raddr(),
			 .o_rdata(),
			 .i_ce()
			 );
   
   // User logic ends
endmodule
